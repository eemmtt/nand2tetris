// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:
    //'zx' and 'nx' logic
    Mux16(a=x, b=false, sel=zx, out=Xz);
    Not16(in=Xz, out=XzNot);
    Mux16(a=Xz, b=XzNot, sel=nx, out=Xzn);
    //'zy' and 'ny' logic
    Mux16(a=y, b=false, sel=zy, out=Yz);
    Not16(in=Yz, out=YzNot);
    Mux16(a=Yz, b=YzNot, sel=ny, out=Yzn);
    //'f' logic
    Add16(a=Xzn, b=Yzn, out=XplusY);
    And16(a=Xzn, b=Yzn, out=XandY);
    Mux16(a=XandY, b=XplusY, sel=f, out=XYf);
    //'no' logic + output for 'ng' and 'zr' logic
    Not16(in=XYf, out=XYfNot);
    Mux16(
        a=XYf, b=XYfNot, sel=no, 
        out[0..7]=outFrontHalf,
        out[8..15]=outBackHalf,
        out[15]=ng,     //'ng' output
        out=out
        );
    //'zr' output
    Or8Way(in=outFrontHalf, out=outFrontHalfOr);
    Or8Way(in=outBackHalf, out=outBackHalfOr);
    Or(a=outFrontHalfOr, b=outBackHalfOr, out=zrNot);
    Not(in=zrNot, out=zr);
}